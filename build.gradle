
plugins {
	id 'com.github.ben-manes.versions' version '0.13.0'
//	id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'groovy'
apply plugin: 'maven'

group 'io.gitlab.arturbosch.smartsmells'
version 'RC1'

repositories {
	mavenCentral()
	mavenLocal()
	maven { url "http://dl.bintray.com/arturbosch/code-analysis" }
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: uri('../repo'))
		}
	}
}

task fatJar(type: Jar) {
	manifest {
		attributes 'Implementation-Title': 'SmartSmells',
				'Implementation-Version': version,
				'Main-Class': 'io.gitlab.arturbosch.smartsmells.Main'
	}
	baseName = project.name
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

test {
	exclude '**/system/**'
	testLogging {
		exceptionFormat = 'full'
	}
}

task systemTest( type: Test ) {
	exclude '/io/gitlab/arturbosch/**'
}

task unitTest( type: Test ) {
	exclude '**/system/**'
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.8'
	compile 'com.github.javaparser:javaparser-core:3.1.0-beta.1'
	compile 'io.gitlab.arturbosch.jpal:jpal:1.0.RC4.b2'
	compile 'org.yaml:snakeyaml:1.17'
	compile 'com.beust:jcommander:1.60'
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'
}
