plugins {
	id "org.sonarqube" version "2.7"
	id "com.jfrog.bintray" version "1.8.0"
	id "com.github.johnrengelman.shadow" version "4.0.4"
	id 'com.github.ben-manes.versions' version '0.20.0'
//	id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'application'

mainClassName = 'io.gitlab.arturbosch.smartsmells.Main'

repositories {
	mavenCentral()
	mavenLocal()
	maven { url "http://dl.bintray.com/arturbosch/code-analysis" }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

publishing {
	publications {
		jqualityPublication(MavenPublication) {
			artifact sourcesJar
			from components.java
			groupId project.group
			artifactId project.name
			version project.version
		}
	}
}

bintray {
	user = System.getenv("BINTRAY_USER") ?: ""
	key = System.getenv("BINTRAY_API_KEY") ?: ""
	publications = ["jqualityPublication"]

	pkg {
		repo = 'code-analysis'
		name = 'jquality'
		userOrg = 'arturbosch'
		licenses = ['Apache-2.0']
		vcsUrl = "https://github.com/arturbosch/jquality"

		version {
			name = project.version
			released = new Date()
		}
	}
}

test {
	exclude '**/system/**'
	testLogging {
		exceptionFormat = 'full'
	}
}

task systemTest(type: Test) {
	exclude '/io/gitlab/arturbosch/**'
}

task unitTest(type: Test) {
	exclude '**/system/**'
}

dependencies {
	compile "io.gitlab.arturbosch.jpal:jpal:$jpalVersion"
	compile "org.yaml:snakeyaml:$yamlVersion"
	compile "com.beust:jcommander:$jcommanderVersion"
	testCompile "org.spockframework:spock-core:$spockVersion"
}
